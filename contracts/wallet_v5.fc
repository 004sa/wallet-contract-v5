#pragma version =0.4.4;

#include "imports/stdlib.fc";

const int error::invalid_c5_action = 37;
const int error::extension_wrong_workchain = 45;
const int error::add_extension_error = 39;
const int error::remove_extension_error = 40;
const int error::disable_signature_when_extensions_is_empty = 44;
const int error::this_signature_mode_already_set = 43;
const int error::remove_last_extension_when_signature_disabled = 42;
const int error::unspported_action = 41;
const int error::signature_disabled = 32;
const int error::invalid_signature = 35;
const int error::invalid_seqno = 33;
const int error::invalid_wallet_id = 34;
const int error::expired = 36;

const int size::bool = 1;
const int size::seqno = 32;
const int size::wallet_id = 80;
const int size::public_key = 256;
const int size::valid_until = 32;
const int size::message_flags = 4;
const int size::signature = 512;
const int size::message_type_prefix = 32;
const int size::address_hash_size = 256;

() return_if(int condition) impure asm "IFRET";
() return_unless(int condition) impure asm "IFNOTRET";

(slice, int) begin_parse_raw(cell c) asm "XCTOS";

cell preload_dict(slice s) asm "PLDDICT";

slice enforce_and_remove_signed_external_prefix(slice body) impure asm "x{7369676E} SDBEGINS";
(slice, int) check_and_remove_extension_action_prefix(slice body) impure asm "x{6578746E} SDBEGINSQ";
(slice, int) check_and_remove_signed_internal_prefix(slice body) impure asm "x{73696E74} SDBEGINSQ";

(slice, int) check_and_remove_add_extension_prefix(slice body) impure asm "x{1c40db9f} SDBEGINSQ";
(slice, int) check_and_remove_remove_extension_prefix(slice body) impure asm "x{5eaef4a4} SDBEGINSQ";
(slice, int) check_and_remove_set_signature_allowed_prefix(slice body) impure asm "x{20cbb95a} SDBEGINSQ";

slice enforce_and_remove_action_send_msg_prefix(slice body) impure asm "x{0ec3c86d} SDBEGINS";

;; Stores pre-computed list of actions (mostly `action_send_msg`) in the actions register.
() set_c5_actions(cell action_list) impure asm "c5 POP";

int count_trailing_zeroes(slice cs) asm "SDCNTTRAIL0";
int count_trailing_ones(slice cs) asm "SDCNTTRAIL1";

slice get_last_bits(slice s, int n) asm "SDCUTLAST";
slice remove_last_bits(slice s, int n) asm "SDSKIPLAST";

cell verify_c5_actions(cell c5, int is_external) inline {
  (slice cs, _) = c5.begin_parse_raw();
  return_if(cs.slice_empty?());

  do {
    ;; only send_msg is allowed, set_code or reserve_currency are not
    cs = cs.enforce_and_remove_action_send_msg_prefix();
    ;; enforce that send_mode has 2 bit set, for that load 7 bits and make sure that they end with 1
    throw_if(error::invalid_c5_action, is_external & count_trailing_zeroes(cs.preload_bits(7)));
    (cs, _) = cs.preload_ref().begin_parse_raw();
  } until (cs.slice_empty?());

  return c5;
}

() process_actions(slice cs, int is_external) impure inline_ref {
  ;; Loop extended actions until we reach standard actions
  while (cs~load_int(1)) {
    int is_add_extension = cs~check_and_remove_add_extension_prefix();
    int is_remove_extension = is_add_extension ? 0 : cs~check_and_remove_remove_extension_prefix();
    ;; Add/remove extensions
    if (is_add_extension | is_remove_extension) {
      (int address_wc, int address_hash) = parse_std_addr(cs~load_msg_addr());
      (int my_address_wc, _) = parse_std_addr(my_address());

      throw_unless(error::extension_wrong_workchain, my_address_wc == address_wc);

      slice data_slice = get_data().begin_parse();
      slice data_slice_before_extensions = data_slice~load_bits(size::bool + size::seqno + size::wallet_id + size::public_key);
      int is_signature_allowed = data_slice_before_extensions.preload_int(size::bool);
      cell extensions = data_slice.preload_dict();

      ;; Add extension
      if (is_add_extension) {
        (extensions, int is_success) = extensions.udict_add_builder?(size::address_hash_size, address_hash, begin_cell().store_int(-1, 1));
        throw_unless( error::add_extension_error, is_success);
      } else { ;; Remove extension
        (extensions, int is_success) = extensions.udict_delete?(size::address_hash_size, address_hash);
        throw_unless(error::remove_extension_error, is_success);
        throw_if(error::disable_signature_when_extensions_is_empty, null?(extensions) & (~ is_signature_allowed));
      }

      set_data(begin_cell()
              .store_slice(data_slice_before_extensions)
              .store_dict(extensions)
              .end_cell());

    } elseif (cs~check_and_remove_set_signature_allowed_prefix()) {
      int allow_signature = cs~load_int(1);
      slice data_slice = get_data().begin_parse();
      int is_signature_allowed = data_slice~load_int(size::bool);
      slice data_tail = data_slice; ;; seqno, wallet_id, public_key, extensions
      if (allow_signature) { ;; allow
        throw_if(error::this_signature_mode_already_set, is_signature_allowed);
        is_signature_allowed = true;
      } else { ;; disallow
        throw_unless(error::this_signature_mode_already_set, is_signature_allowed);
        data_slice = data_slice.skip_bits(size::seqno + size::wallet_id + size::public_key);
        int is_extensions_not_empty = data_slice.preload_uint(1);
        throw_unless(error::remove_last_extension_when_signature_disabled, is_extensions_not_empty);
        is_signature_allowed = false;
      }
      set_data(begin_cell()
        .store_int(is_signature_allowed, size::bool)
        .store_slice(data_tail) ;; wallet_id, public_key, extensions
        .end_cell());
    } else {
      throw(error::unspported_action);
    }
    cs = cs.preload_ref().begin_parse();
  }
  ;; Simply set the C5 register with all pre-computed actions after verification:
  set_c5_actions(cs.preload_ref().verify_c5_actions(is_external));
}

;; ------------------------------------------------------------------------------------------------

() process_signed_request(slice in_msg_body, int is_external) impure inline {
  ifnot (is_external) {
    ;; Additional check to make sure that there are enough bits for reading (+1 for actual actions flag)
    return_if(in_msg_body.slice_bits() < size::message_type_prefix + size::bool + size::wallet_id + size::valid_until + size::seqno + 1 + size::signature);
  }

  slice signature = in_msg_body.get_last_bits(size::signature);
  slice signed_slice = in_msg_body.remove_last_bits(size::signature);

  slice cs = signed_slice.skip_bits(size::message_type_prefix); ;; skip signed_internal or signer_external prefix
  (int wallet_id, int valid_until, int seqno) = (cs~load_uint(size::wallet_id), cs~load_uint(size::valid_until), cs~load_uint(size::seqno));

  slice data_slice = get_data().begin_parse();
  int is_signature_allowed = data_slice~load_int(size::bool);

  int stored_seqno = data_slice~load_uint(size::seqno);
  slice data_tail = data_slice; ;; wallet_id, public_key, extensions
  int stored_wallet_id = data_slice~load_uint(size::wallet_id);
  int public_key = data_slice.preload_uint(size::public_key);

  int is_signature_valid = check_signature(slice_hash(signed_slice), signature, public_key);
  ifnot (is_signature_valid) {
    if (is_external) {
      throw_unless(error::invalid_signature, is_signature_valid);
    } else {
      return ();
    }
  }
  throw_unless(error::signature_disabled, is_signature_allowed);
  throw_unless(error::invalid_seqno, seqno == stored_seqno);
  throw_unless(error::invalid_wallet_id, wallet_id == stored_wallet_id);
  throw_if(error::expired, valid_until <= now());

  if (is_external) {
    accept_message();
  }

  ;; Store and commit the seqno increment to prevent replays even if the subsequent requests fail.
  stored_seqno = stored_seqno + 1;
  set_data(begin_cell()
    .store_int(true, size::bool) ;; is_signature_allowed
    .store_uint(stored_seqno, size::seqno)
    .store_slice(data_tail) ;; wallet_id, public_key, extensions
    .end_cell());

  commit();

  process_actions(cs, is_external);
}

() recv_external(slice in_msg_body) impure inline {
  slice body = in_msg_body;
  in_msg_body = enforce_and_remove_signed_external_prefix(in_msg_body);
  process_signed_request(body, true);
}

;; ------------------------------------------------------------------------------------------------

() recv_internal(cell in_msg_full, slice in_msg_body) impure inline {
  return_if(in_msg_body.slice_refs_empty?()); ;; message with actions always have a ref

  slice in_msg_full_slice = in_msg_full.begin_parse();

  slice message_flags_slice = in_msg_full_slice~load_bits(size::message_flags); ;; int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool

  ;; skip bounced messages - if bounced flag (last bit) is set amount of trailing ones will be non-zero, else it will be zero.
  return_if(count_trailing_ones(message_flags_slice));

  (in_msg_body, int is_extension_action) = check_and_remove_extension_action_prefix(in_msg_body);

  if (is_extension_action) {

    ;; Authenticate extension by its address.
    (int sender_address_wc, int sender_address_hash) = parse_std_addr(in_msg_full_slice~load_msg_addr());
    (int my_address_wc, _) = parse_std_addr(my_address());

    return_unless(my_address_wc == sender_address_wc);

    slice data_slice = get_data().begin_parse();
    cell extensions = data_slice.skip_bits(size::bool + size::seqno + size::wallet_id + size::public_key).preload_dict();

    ;; Note that some random contract may have deposited funds with this prefix,
    ;; so we accept the funds silently instead of throwing an error (wallet v4 does the same).
    (_, int extension_found) = extensions.udict_get?(size::address_hash_size, sender_address_hash);
    return_unless(extension_found);

    process_actions(in_msg_body, false);
    return ();

  }

  slice body = in_msg_body;
  (_, int is_signed_internal) = check_and_remove_signed_internal_prefix(in_msg_body);
  return_unless(is_signed_internal);

  process_signed_request(body, false);
}

;; ------------------------------------------------------------------------------------------------
;; Get methods

int get_is_signature_auth_allowed() method_id {
  return get_data().begin_parse()
    .preload_int(size::bool);
}

int seqno() method_id {
  return get_data().begin_parse()
    .skip_bits(size::bool)
    .preload_uint(size::seqno);
}

int get_wallet_id() method_id {
  return get_data().begin_parse()
    .skip_bits(size::bool + size::seqno)
    .preload_uint(size::wallet_id);
}

int get_public_key() method_id {
  return get_data().begin_parse()
    .skip_bits(size::bool + size::seqno + size::wallet_id)
    .preload_uint(size::public_key);
}

;; Returns raw dictionary (or null if empty) where keys are address hashes. Workchains of extensions are same with wallet smart contract workchain
cell get_extensions() method_id {
  return get_data().begin_parse()
    .skip_bits(size::bool + size::seqno + size::wallet_id + size::public_key)
    .preload_dict();
}